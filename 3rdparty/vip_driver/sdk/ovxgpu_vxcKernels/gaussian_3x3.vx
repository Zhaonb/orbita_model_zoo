__kernel void gaussian_3x3_U8toU8
    (
    __read_only  image2d_t input,
    __write_only image2d_t output
    )
{
    int4 coord = (int4)(get_global_id(0), get_global_id(1), get_global_id(0) - 1, get_global_id(0) + 1);

    uint4 top, med, bot;

    top.x = read_imageui(input, (int2)(coord.z, coord.y - 1)).x;
    top.y = read_imageui(input, (int2)(coord.x, coord.y - 1)).x;
    top.z = read_imageui(input, (int2)(coord.w, coord.y - 1)).x;
    med.x = read_imageui(input, (int2)(coord.z, coord.y + 0)).x;
    med.y = read_imageui(input, (int2)(coord.x, coord.y + 0)).x;
    med.z = read_imageui(input, (int2)(coord.w, coord.y + 0)).x;
    bot.x = read_imageui(input, (int2)(coord.z, coord.y + 1)).x;
    bot.y = read_imageui(input, (int2)(coord.x, coord.y + 1)).x;
    bot.z = read_imageui(input, (int2)(coord.w, coord.y + 1)).x;

    uint4 dst;

    dst.xyz = top.xyz * (uint3)(1, 2, 1) + med.xyz * (uint3)(2, 4, 2) + bot.xyz * (uint3)(1, 2, 1);
    dst.x = clamp((dst.x + dst.y + dst.z) >> 4, 0, 255);

    write_imageui(output, coord.xy, dst.xxxx);
}
